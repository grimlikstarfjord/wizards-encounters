buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

repositories {
    google()
    mavenCentral()
}

android {
    namespace 'com.alderangaming.wizardsencounters'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.alderangaming.wizardsencounters"
        minSdkVersion 8
        targetSdkVersion 33
        versionCode 91
        versionName "0.91"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            // default debug signing
        }
    }

    // Name APKs using versionName/versionCode from AndroidManifest.xml
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def manifestFile = file('AndroidManifest.xml')
            def text = manifestFile.getText('UTF-8')
            def mName = (text =~ /android:versionName\s*=\s*"([^"]+)"/)
            def mCode = (text =~ /android:versionCode\s*=\s*"(\d+)"/)
            def vName = mName.find() ? mName.group(1) : variant.versionName
            def vCode = mCode.find() ? Integer.parseInt(mCode.group(1)) : variant.versionCode
            def baseName = "WizardsEncounters-${variant.buildType.name}-v${vName}(${vCode})"
            outputFileName = baseName + ".apk"
        }
    }
}

dependencies {
}


